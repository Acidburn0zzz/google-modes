start top { Statement* }

skip whitespace {
  context Statement {
    var VariableDeclarator ("," VariableDeclarator)* semi |
    whileWith ParenExpr Statement |
    Block |
    ";" |
    debugger |
    if ParenExpr Statement (else Statement)? |
    function asyncStar? declName FunctionDef |
    for ForSpec Statement |
    Label |
    return (semi | Expression semi) |
    throw Expression semi |
    breakCont identifier? semi |
    switch ParenExpr Block |
    try Block (catch "(" declName ")" Block)? (finally Block)? |
    class declName ClassDef |
    Export |
    Import |
    async Statement |
    "@" Expression Statement |
    Expression semi
  }

  VariableDeclarator { Pattern (op("=") ExpressionNoComma)? }

  context Block { "{" Statement* "}" }

  Label { (default | labelName | case Expression) ":" }

  context ForSpec {
    "(" Statement (kw("of") Expression | Expression? ";" Expression?)? ")"
  }

  Export {
    export
    (("*" | default | "{" CommaSep(DeclMaybeAs) "}") (kw("from") string)? |
     Statement)
    semi
  }

  Import {
    import
    (string |
     ((asyncStar (kw("as") declName)? | DeclMaybeAs | "{" CommaSep(DeclMaybeAs) "}")
      (kw("from") string)?))
    semi
  }

  DeclMaybeAs {
    variableAs kw("as") declName | declName
  }

  Expression { BaseExpression ("," Expression | ExpressionSuffix(Expression))* }

  ExpressionNoComma { BaseExpression ExpressionSuffix(ExpressionNoComma)* }

  context ParenExpr { "(" Expression ")" }

  // FIXME fat arrows
  BaseExpression {
    atom |
    kwExpr |
    (kwPrefix | prefixOp | async) BaseExpression |
    new ("." kw("target") | BaseExpression) |
    function asyncStar? declName? FunctionDef |
    class (!kw("extends") declName)? ClassDef |
    variable |
    number |
    string |
    regexp |
    template |
    ArrayLiteral |
    ObjectLiteral |
    ParenExpr
  }

  context ArrayLiteral { "[" CommaSep(ExpressionNoComma) "]" }

  context ObjectLiteral {
    "{" CommaSep(ObjectProp) "}"
  }

  ObjectProp {
    propModifier?
    ((propName | "[" Expression "]" | number | string) (asyncStar? FunctionDef | ":" ExpressionNoComma)? |
     "..." ExpressionNoComma)
  }

  ExpressionSuffix(ContExpression) {
    arrow (Block | ContExpression) |
    suffixOp |
    (binOp | kwBinOp) ContExpression |
    ArgList |
    template |
    "." propName |
    "[" Expression "]" |
    op("?") Expression op(":") ContExpression
  }

  context ArgList {
    "(" CommaSep(ExpressionNoComma) ")"
  }

  context ParamList {
    "(" CommaSep(Pattern) ")"
  }

  context FunctionDef {
    ParamList Block
  }

  ClassDef {
    (extends Expression)? ClassBody
  }

  context ClassBody {
    "{" (kw("static")? ObjectProp | "@" Expression)* "}"
  }

  Pattern {
    declName | op("...") Pattern | ArrayPattern | ObjectPattern
  }

  context ArrayPattern { "[" CommaSep(Pattern) "]" }

  context ObjectPattern { "{" CommaSep(PropPattern) "}" }

  PropPattern {
    declNameNoColon |
    propName ":" Pattern (op("=") ExpressionNoComma)? |
    "..." Pattern
  }

  CommaSep(expr) { (expr ("," expr?)*)? }
}

tokens {
  var { kw("var" | "let" | "const") }
  whileWith { kw("while" | "with") }
  else { kw("else") }
  do { kw("do") }
  try { kw("try") }
  finally { kw("finally") }
  return { kw("return") }
  throw { kw("throw") }
  breakCont { kw("break" | "continue") }
  debugger { kw("debugger") }
  case { kw("case") }
  default { kw("default") }
  function { kw("function") }
  if { kw("if") }
  catch { kw("catch") }
  for { kw("for") }
  switch { kw("switch") }
  class { kw("class") }
  export { kw("export") }
  import { kw("import") }
  extends { kw("extends") }
  async { kw("async") ~(whitespace function) }
  new { kw("new") }
  kwBinOp { kw("in" | "instanceof") }
  kwPrefix { kw("delete" | "typeof" | "yield" | "await") }
  kwExpr { kw("super" | "this") }

  atom="atom" { ("true" | "false" | "null" | "undefined" | "NaN" | "Infinity") !identifierChar }

  variable="variable" { identifier }

  arrow="operator" { "=>" }
  prefixOp="operator" { "..." | "!" | "+" "+"? | "-" "-"? }
  binOp="operator" { ("+" | "-" | "%" | "*" | "/" | ">" ">"? ">"? | "<" "<"? | "=" | "&" "&"? | "|" "|"? | "^") "="? }
  suffixOp="operator" { "++" | "--" }

  string="string" {
    "'" ("\\" _ | (!"'" .))* "'" |
    '"' ("\\" _ | (!'"' .))* '"'
  }

  number="number" {
    "0x" (digit | "a"-"f" | "A"-"F")+ |
    "0o" "0"-"7"+ |
    "0b" ("0" | "1")+ |
    (digit+ ("." digit*)? | "." digit+) (("e" | "E") ("+" | "-")? digit+)?
  }

  comment="comment" { "/*" (!"*/" _)* "*/" | "//" .* }

  regexp="string-2" { "/" ("\\" . | .)+ "/" }

  template {
    str2("`")
    (str2("${") Expression str2("}") | str2("\\" _ | "\n") | str2((!("`" | "${" | "\\") .)+))*
    str2("`")
  }
}

whitespace { (spaceChar | comment)* }

kw(value)="keyword" { value !identifierChar }
op(value)="operator" { value }
str2(value)="string-2" { value }

propName="property" { identifier }
declName="def" { identifier }
labelName { identifier ~(" "* ":") }

declNameNoColon { declName !(identifierChar | " "* ":") }

propModifier { kw("get" | "set" | "async") !(" "* ("," | "}" | ":")) }

asyncStar="keyword" { "*" }

variableAs { variable ~(" "* "as") }

identifier { identifierStart identifierChar* }

semi { ";" | ~"}" | &startOfLine } // FIXME

letter { "a"-"z" | "A"-"Z" | "\xa1"-"\uffff" | "_" }

digit { "0"-"9" }

identifierStart { letter | "_" | "$" }

identifierChar { letter | digit | "_" | "$" }

spaceChar { "\n" | "\t" | " " }
